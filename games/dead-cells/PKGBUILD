# Maintainer: Hoream <hoream@qq.com>
pkgname=dead-cells
pkgver=2023.3.6
pkgrel=6
pkgdesc="a rogue-lite, metroidvania inspired, action-platformer"
arch=('x86_64' 'aarch64')
url="https://www.linuxgame.cn/dead-cells"
license=('custom')
depends=('bubblewrap' 'libxss' 'libbsd' 'hashlink')
source=("Dead Cells.zip::http://app.hoream.top:8000/games/Dead%20Cells.zip"
        "gog_com-Dead_Cells.desktop")
sha256sums=('97ec0b5a7243d2bac5e7319c8e3ab7298bca1a219249b31cf2462e3e99a1ae20'
    '05aa7f3c11f6f5e6ca67a902594ec2cf8c3c8e121fe07b1ab0f12ed7b60885d1')
options=(!strip)
install="dead-cells.install"

if [[ $CARCH == "aarch64" ]]; then
    depends+=('box64')
fi

package() {
	install -d "${pkgdir}/opt/gog/${pkgname}"
	install -Dm644 "$srcdir"/gog_com-Dead_Cells.desktop "$pkgdir"/usr/share/applications/gog_com-Dead_Cells.desktop
	cp -r "$srcdir"/* "${pkgdir}/opt/gog/${pkgname}"

    rm "${pkgdir}/opt/gog/${pkgname}/uninstall-Dead Cells.sh"
    rm "${pkgdir}/opt/gog/${pkgname}/gog_com-Dead_Cells.desktop"
    rm "${pkgdir}/opt/gog/${pkgname}/Dead Cells.zip"

	sed -i '/chmod +x */d' "$pkgdir"/opt/gog/"${pkgname}"/start.sh
    find ${pkgdir}/opt/gog/${pkgname} -type d -exec chmod 755 {} \;

    sed -i 's|\./"deadcells\.sh"|mkdir -p ${HOME}/.local/share/dead-cells\n bwrap --ro-bind / / \\\n\t --bind /run /run \\\n\t --dev-bind /dev /dev \\\n\t --bind ${HOME}/.local/share/dead-cells /opt/gog/dead-cells/game/save \\\n\t ./"deadcells.sh"|g' "$pkgdir"/opt/gog/"${pkgname}"/start.sh

    # enable wayland support
    rm "${pkgdir}/opt/gog/${pkgname}/game/sdl.hdll"
    rm "${pkgdir}/opt/gog/${pkgname}/game/libSDL2-2.0.so.0"
    rm "${pkgdir}/opt/gog/${pkgname}/game/libhl.so"
    sed -i '2i\
if [ "$XDG_SESSION_TYPE" = "wayland" ]; then\
    export SDL_VIDEODRIVER=wayland\
else \
    export SDL_VIDEODRIVER=x11\
fi
' "$pkgdir"/opt/gog/"${pkgname}"/game/deadcells.sh

    if [[ $CARCH == "aarch64" ]]; then
        sed -i 's|./deadcells|box64 ./deadcells|' "$pkgdir"/opt/gog/"${pkgname}"/game/deadcells.sh 
    fi
}
