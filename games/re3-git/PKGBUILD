_pkgbase=re3
pkgname=re3-git
pkgver=r3167.3233ffe1
pkgrel=2
pkgdesc="An open-source project reverse-engineering Grand Theft Auto III"
arch=('x86_64' 'aarch64')
url="https://github.com/GTAmodding/re3"
license=('custom:none')
# glfw-wayland will failed when runing.
depends=('openal' 'glew' 'glfw-x11' 'mpg123' 'zenity')
makedepends=('git' 'premake')
provides=("$_pkgbase")
conflicts=("$_pkgbase")
source=(
    "re3.bundle::https://archive.org/download/github.com-GTAmodding-re3_-_2021-09-06_14-11-00/GTAmodding-re3_-_2021-09-06_14-11-00.bundle"
    "git+https://github.com/aap/librw.git"
    "git+https://github.com/xiph/ogg.git"
    "git+https://github.com/xiph/opus.git"
    "git+https://github.com/xiph/opusfile.git"
    'no_link_with_unnecessary_sndfile.patch'
    're3-launcher'
    're3.desktop'
    're3_data.tar.gz::http://app.hoream.top:8000/games/re3_data.tar.gz'
)
sha256sums=('035cb5f59811ae086510f02bd3817eaf43933c9723e52dba60807f48c4e9d9c3'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '1d079928163893daa219a14cd3fa50de86b916ecbbeccb9587173d4b35d8e532'
            'SKIP'
            '7d19322282de244decac39a1b3aa54196b5f9ba3e3762dbde931c75710bf9931'
            'SKIP')
options=(!strip)

pkgver() {
  cd "$srcdir/$_pkgbase"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  git clone re3.bundle "$srcdir/$_pkgbase"
  cd "$srcdir/$_pkgbase"
  git submodule init
  for submod in librw ogg opus opusfile
  do
    git config "submodule.vendor/$submod.url" "../$submod"
  done
  git -c protocol.file.allow=always submodule update
  patch -uNp1 -i ../no_link_with_unnecessary_sndfile.patch
}

build() {
  cd "$srcdir/$_pkgbase"
  premake5 --with-librw gmake2
  ./printHash.sh src/extras/GitSHA1.cpp
  if [[ $CARCH == "aarch64" ]]; then
    make -C build config=release_linux-arm64-librw_gl3_glfw-oal
  else
    make -C build config=release_linux-amd64-librw_gl3_glfw-oal
  fi

}

package() {
  cd "$srcdir/$_pkgbase"
  if [[ $CARCH == "aarch64" ]]; then
      install -D -m755 -t "$pkgdir/usr/bin" \
        "bin/linux-arm64-librw_gl3_glfw-oal/Release/re3" \
        ../re3-launcher
  else
      install -D -m755 -t "$pkgdir/usr/bin" \
        "bin/linux-amd64-librw_gl3_glfw-oal/Release/re3" \
        ../re3-launcher
  fi

  install -D -m644 -t "$pkgdir/usr/share/applications" ../re3.desktop
  install -D -m644 res/images/logo.svg "$pkgdir/usr/share/pixmaps/re3.svg"
  mkdir -p "$pkgdir/usr/share/games/re3"
  cp -a gamefiles "$pkgdir/usr/share/games/re3/"
  chmod og=rX -R "$pkgdir/usr/share/games/re3"

  mkdir -p "$pkgdir/usr/share/games/re3/data"
  cp $srcdir/RE3/* "$pkgdir/usr/share/games/re3/data" -r

  # only support x11
  sed -i '2i\export SDL_VIDEODRIVER=x11' "$pkgdir"/usr/bin/re3-launcher

}
