# Maintainer: Hoream <hoream@qq.com>

_pkgname=kvantum
pkgname=kvantum-git
pkgver=1.0.10
pkgrel=3
pkgdesc='SVG-based theme engine for Qt5/6 (including config tool and extra themes)'
arch=(x86_64)
url='https://github.com/tsujan/Kvantum'
license=(GPL)
depends=(kwindowsystem5
         qt5-svg)
makedepends=(cmake
             qt5-tools
             qt6-svg
             qt6-tools
             git)
optdepends=('qt6-svg: Qt6 style')
source=(${_pkgname}::git+https://github.com/tsujan/Kvantum.git)
sha256sums=('SKIP')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

prepare() {
  # Fix Qt6 build with Qt5 installed
  sed -e 's|Qt6 Qt5|Qt6|' -i ${_pkgname}/Kvantum/*/CMakeLists.txt
}

build() {
  cmake -B build5 -S ${_pkgname}/Kvantum \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build5

  cmake -B build6 -S ${_pkgname}/Kvantum \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT5=OFF
  make -C build6
}

package() {
  DESTDIR="$pkgdir" cmake --install build5 
  DESTDIR="$pkgdir" cmake --install build6
}
