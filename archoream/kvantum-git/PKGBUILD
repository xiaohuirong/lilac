# Maintainer: Hoream <hoream@qq.com>

_pkgname=kvantum
pkgname=kvantum-git
pkgver=1.1.0.r2.g4ea962c9
pkgrel=1
pkgdesc='SVG-based theme engine for Qt5/6 (including config tool and extra themes)'
arch=(x86_64)
url='https://github.com/tsujan/Kvantum'
license=(GPL)
depends=(kwindowsystem
         qt5-svg)
makedepends=(cmake
             qt5-tools
             qt6-svg
             qt6-tools
             git)
optdepends=('qt6-svg: Qt6 style')
source=(${_pkgname}::git+https://github.com/tsujan/Kvantum.git)
sha256sums=('SKIP')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

prepare() {
  # Fix Qt6 build with Qt5 installed
  sed -e 's|Qt6 Qt5|Qt6|' -i ${_pkgname}/Kvantum/*/CMakeLists.txt
}

pkgver() {
  cd "${srcdir}/${_pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/V//' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cmake -B build5 -S ${_pkgname}/Kvantum \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build5

  cmake -B build6 -S ${_pkgname}/Kvantum \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT5=OFF
  make -C build6
}

package() {
  DESTDIR="$pkgdir" cmake --install build5 
  DESTDIR="$pkgdir" cmake --install build6
}
