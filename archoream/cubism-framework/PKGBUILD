# Maintainer: Hoream <hoream@qq.com>
pkgname=cubism-framework
pkgver=5.0.0
pkgrel=2
pkgdesc="Cubism Framework library"
url="https://github.com/Live2D/CubismNativeFramework"
license=("custom")
arch=("x86_64")
provides=(cubism-core)
conflicts=()
depends=(cubism-core qt6-base)
makedepends=(cmake git)
source=("git+https://github.com/xiaohuirong/CubismNativeFramework.git#branch=qt6"
        "CubismFrameworkConfig.cmake"
        "cubism-framework.pc")
sha256sums=("SKIP"
            "SKIP"
            "SKIP")

prepare() {
    cd "${srcdir}"
    sed -i "1i Version: ${pkgver}" "cubism-framework.pc"
    sed -i "1i set(CubismFramework_VERSION \"${pkgver}\")" "CubismFrameworkConfig.cmake"
}

build() {
    cd "${srcdir}/CubismNativeFramework"
    mkdir build
    cd build
    cmake .. \
    -DFRAMEWORK_SOURCE=OpenGL \
    -DCMAKE_CXX_FLAGS="-DCSM_TARGET_LINUX_GL"
    make
}

package() {
    install -d "${pkgdir}/usr/lib/cubism/framework"    
    install -d "${pkgdir}/usr/lib/pkgconfig"    
    install -d "${pkgdir}/usr/lib/cmake/CubismFramework/"    

    install -Dm755 "${srcdir}/CubismNativeFramework/build/libFramework.so" "${pkgdir}/usr/lib/cubism/framework/libFramework.so"
    install -Dm644 "${srcdir}/cubism-framework.pc" "${pkgdir}/usr/lib/pkgconfig/cubism-framework.pc"
    install -Dm644 "${srcdir}/CubismFrameworkConfig.cmake" "${pkgdir}/usr/lib/cmake/CubismFramework/CubismFrameworkConfig.cmake"

    local base_dir="${pkgdir}/usr/include/cubism/framework"
    local subdirs=("Type" "Utils" "Math" "Rendering" "Rendering/OpenGL" "Model" "Effect" "Id" "Motion" "Physics")
    install -d "${base_dir}" 
    install -Dm644 "${srcdir}/CubismNativeFramework/src/"*.hpp "${base_dir}/"
    for subdir in "${subdirs[@]}"; do
        install -d "${base_dir}/${subdir}"
        install -Dm644 "${srcdir}/CubismNativeFramework/src/${subdir}/"*.hpp "$base_dir/$subdir/"
    done
    install -Dm644 "${srcdir}/CubismNativeFramework/src/Rendering/CubismClippingManager.tpp" "$base_dir/Rendering/"
}
