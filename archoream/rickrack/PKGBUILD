# Maintainer: Hoream <hoream@qq.com>

pkgname="python-rickrack"
_pkgname=${pkgname#python-}
pkgver=r67.766dc7c
pkgrel=1
pkgdesc="Generate harmonious colors freely."
url="https://github.com/eigenmiao/Rickrack"
license=("GPL3")
arch=('x86_64')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=(python)
makedepends=(git python-build python-installer python-wheel)
source=(${_pkgname}::git+${url})
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )

}

build() {
    cd "$_pkgname"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_pkgname"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
