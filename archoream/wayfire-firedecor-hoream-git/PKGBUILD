# Maintainer: Hoream <hoream@qq.com>
_pkgname=wayfire-firedecor
pkgname=wayfire-firedecor-hoream-git
pkgver=r89.3c6777d
pkgrel=2
pkgdesc="An advanced window decoration plugin for the wayfire window manager."
arch=('x86_64' 'aarch64')
url="https://github.com/AhoyISki/wayfire-firedecor.git"
license=('MIT')
depends=('wayfire-hoream-git' 'librsvg')
makedepends=('git' 'meson' 'ninja' 'boost')
provides=("${_pkgname}")
#conflicts=("${_pkgname}" "${_pkgname}-git")
source=("${_pkgname}::git+https://github.com/AhoyISki/${_pkgname}.git#commit=3c6777dc64bc2e62ea495a8c99a712dee8c20edb")
sha256sums=("SKIP")

pkgver() {
  cd "${srcdir}/${_pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
    cd "${srcdir}/${_pkgname}"
    export PKG_CONFIG_PATH=/opt/wlroots-hoream/lib/pkgconfig:/opt/wayfire-hoream/lib/pkgconfig:$PKG_CONFIG_PATH
    export C_INCLUDE_PATH=/opt/wlroots-hoream/include:/opt/wayfire-hoream/include:$C_INCLUDE_PATH
    export CPLUS_INCLUDE_PATH=/opt/wlroots-hoream/include:/opt/wayfire-hoream/include:$CPLUS_INCLUDE_PATH

	arch-meson \
    --datadir /opt/wayfire-hoream/share \
    build
    ninja -C build
}

package() {
    cd "${srcdir}/${_pkgname}"
    DESTDIR="${pkgdir}/" ninja -C build install
}
