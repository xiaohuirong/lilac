# Maintainer: Hoream <hoream@qq.com>
pkgname=wf-shell-hoream-git
pkgver=r290.f49f719
pkgrel=1
pkgdesc='GTK3-based panel for the Wayfire compositor'
url=https://wayfire.org
arch=(x86_64 aarch64)
license=(custom:MIT)
provides=("${pkgname%-git}")
conflicts=("${provides[@]}")
depends=(wf-config-hoream-git gtkmm3 gtk-layer-shell libdbusmenu-gtk3 libpulse)
makedepends=("git" "glib2-devel" "gobject-introspection" "libdbusmenu-gtk3" "meson" "ninja" "wayfire-hoream-git" "wayland" "wayland-protocols")
source=("${pkgname}::git+https://github.com/WayfireWM/wf-shell")
sha512sums=('SKIP')

pkgver () {
	cd "${pkgname}"
	(
		set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build () {
    export PKG_CONFIG_PATH=/opt/wlroots-hoream/lib/pkgconfig:/opt/wayfire-hoream/lib/pkgconfig:$PKG_CONFIG_PATH
	rm -rf build
	arch-meson \
    -Dpulse=enabled \
    --prefix=/opt/wayfire-hoream \
    "${pkgname}" build
	ninja -C build
}

package () {
	DESTDIR="${pkgdir}" ninja -C build install
	cd "${pkgname}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 wf-shell.ini.example "${pkgdir}/usr/share/doc/${pkgname}/wf-shell.ini.example"
}
