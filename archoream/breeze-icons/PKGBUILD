# Maintainer: Hoream <hoream@qq.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=breeze-icons
pkgname=('breeze-dark-icons' 'breeze-light-icons')
pkgver=5.111.0
pkgrel=1
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
makedepends=(extra-cmake-modules-git qt5-base python-lxml)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
sha256sums=('4c570e4c5f75a30f911db6fe5d907dfb58a02d2afbd160d81b51bfc8dc5b3aef')

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package_breeze-dark-icons() {
  pkgdesc='Breeze Dark icon themes'
  conflicts=('breeze-icons' 'breeze-light-icons')
  provides=('breeze-icons')


  DESTDIR="$pkgdir" cmake --install build
  rm ${pkgdir}/usr/share/icons/breeze -r
}

package_breeze-light-icons() {
  pkgdesc='Breeze Light icon themes'
  conflicts=('breeze-icons' 'breeze-dark-icons')
  provides=('breeze-icons')

  DESTDIR="$pkgdir" cmake --install build
  rm ${pkgdir}/usr/share/icons/breeze-dark -r
}
