# Maintainer: Hoream <hoream@qq.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=breeze-icons
pkgname=('breeze-dark-icons' 'breeze-light-icons')
pkgdesc='Breeze icon theme'
pkgver=6.20.0
pkgrel=1
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
makedepends=(extra-cmake-modules
             python-lxml
             qt6-base
             rsync)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
sha256sums=('0a47b28a04a086ccb5b4afb51d6677180006819d0d9302524721689bfa4ad13c')

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_breeze-dark-icons() {
  pkgdesc='Breeze Dark icon themes'
  conflicts=('breeze-icons' 'breeze-light-icons')
  provides=('breeze-icons')


  DESTDIR="$pkgdir" cmake --install build
  rsync -av --ignore-existing --exclude='breeze-icons.rcc' $pkgdir/usr/share/icons/breeze/ $pkgdir/usr/share/icons/breeze-dark/
  rm ${pkgdir}/usr/share/icons/breeze -r
  mkdir ${pkgdir}/usr/share/icons/breeze
  cd ${pkgdir}/usr/share/icons/breeze
  ln -s ../breeze-dark/* .
}

package_breeze-light-icons() {
  pkgdesc='Breeze Light icon themes'
  conflicts=('breeze-icons' 'breeze-dark-icons')
  provides=('breeze-icons')

  DESTDIR="$pkgdir" cmake --install build
  rm ${pkgdir}/usr/share/icons/breeze-dark -r
  mkdir ${pkgdir}/usr/share/icons/breeze-dark
  cd ${pkgdir}/usr/share/icons/breeze-dark
  ln -s ../breeze/* .
}
