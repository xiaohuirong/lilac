# Maintainer: Hoream <hoream@qq.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=breeze-icons
pkgname=('breeze-dark-icons' 'breeze-light-icons')
pkgver=5.112.0
pkgrel=1
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
makedepends=(extra-cmake-modules qt5-base python-lxml rsync)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
sha256sums=('69dc51befe11f1431dd5b58b6be304e9ac394f6a4801a9351cf64341946c7d58')

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package_breeze-dark-icons() {
  pkgdesc='Breeze Dark icon themes'
  conflicts=('breeze-icons' 'breeze-light-icons')
  provides=('breeze-icons')


  DESTDIR="$pkgdir" cmake --install build
  rsync -av --ignore-existing --exclude='breeze-icons.rcc' $pkgdir/usr/share/icons/breeze/ $pkgdir/usr/share/icons/breeze-dark/
  rm ${pkgdir}/usr/share/icons/breeze -r
}

package_breeze-light-icons() {
  pkgdesc='Breeze Light icon themes'
  conflicts=('breeze-icons' 'breeze-dark-icons')
  provides=('breeze-icons')

  DESTDIR="$pkgdir" cmake --install build
  rm ${pkgdir}/usr/share/icons/breeze-dark -r
}
