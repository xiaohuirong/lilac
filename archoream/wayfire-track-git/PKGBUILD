# Maintainer: Solomon Choina <shlomochoina@gmail.com>
# Co-Maintainer: Frank Tao <frank.tao@uwaterloo.ca>
_pkgname=wayfire-git
pkgname=wayfire-track-git
pkgver=0.8.0.r46.g553cdc48
pkgrel=1
pkgdesc="3D wayland compositor"
arch=('x86_64')
url="https://github.com/WayfireWM/wayfire"
license=('MIT')
depends=('cairo' 'pango' 'doctest' 'freetype2' 'glm' 'nlohmann-json'
         'libdrm' 'libevdev' 'libglvnd' 'libinput' 'libjpeg'
         'libpng' 'libxkbcommon' 'libxml2' 'pixman' 'polkit'
         'pkgconf' 'seatd' 'xcb-util-errors' 'xcb-util-renderutil'
         'xcb-util-wm' 'xorg-xwayland' 'wayland' 'wayland-protocols'
         'libdisplay-info')
makedepends=('git' 'glslang' 'meson' 'ninja' 'cmake' 'vulkan-headers')
optdepends=('xorg-xeyes')
provides=("${_pkgname%-git}" 'wlroots' 'wf-config' 'wlroots-git' 'wf-config-git' 'libwlroots.so')
conflicts=("$_pkgname" 'wlroots-git' 'wlroots' 'wf-config-git' 'wf-config')
replaces=()
options=()

source=('wayfire::git+https://github.com/WayfireWM/wayfire#branch=track-wlroots'
        'wf-config::git+https://github.com/WayfireWM/wf-config'
        'wf-touch::git+https://github.com/WayfireWM/wf-touch'
        'wf-utils::git+https://github.com/WayfireWM/wf-utils'
        'wlroots::git+https://gitlab.freedesktop.org/wlroots/wlroots'
        )

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
    cd "$srcdir/wayfire"
    git config submodule.subprojects/wf-config.url "${srcdir}/wf-config"
    git -c protocol.file.allow=always submodule update --init subprojects/wf-config

    git config submodule.subprojects/wf-touch.url "${srcdir}/wf-touch"
    git -c protocol.file.allow=always submodule update --init subprojects/wf-touch

    git config submodule.subprojects/wf-utils.url "${srcdir}/wf-utils"
    git -c protocol.file.allow=always submodule update --init subprojects/wf-utils

    git config submodule.subprojects/wlroots.url "${srcdir}/wlroots"
    git -c protocol.file.allow=always submodule update --init subprojects/wlroots
}

pkgver() {
	cd "$srcdir/wayfire"
  tag=$(git tag -l | awk '/^[0-9.]+$/ {print $0} /^v{1}[0-9.]+$/ {print substr($0,2)}'|sort -n|tail -n1)
  printf "$tag.r%s.g%s" "$(git rev-list --count v${tag}..HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/wayfire/"
    #patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/triple_buffer.patch
    #patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/comment.patch

    arch-meson \
        --buildtype=release \
        -Dxwayland=auto \
        -Duse_system_wlroots=disabled \
        -Duse_system_wfconfig=disabled \
        -Db_lto=true \
        -Db_pie=true \
        build
    ninja -C build
    sed "/WF_SRC_DIR/d" -i build/config.h
}


package() {
    cd "$srcdir/wayfire"
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 wayfire.desktop $pkgdir/usr/share/wayland-sessions/wayfire.desktop
    cp wayfire.ini $pkgdir/usr/share
}
