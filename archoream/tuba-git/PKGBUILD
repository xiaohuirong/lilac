# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Fediverse: @fabiscafe@mstdn.social
# Contributor: Luna Jernberg <lunajernberg@gnome.org>
# Fediverse: @bittin@social.vivaldi.net

_pkgname=tuba
pkgname="$_pkgname-git"
pkgver=0.9.1.r72.gb7fcb4d4
pkgrel=1
pkgdesc='Browse the Fediverse'
arch=(
  aarch64 #ALARM
  armv7h  #ALARM
  i686    #Arch Linux32
  x86_64  #Arch Linux
)
url='https://tuba.geopjr.dev/'
license=('GPL3')
depends=(
  dconf
  gdk-pixbuf2
  gspell
  gtk4
  gtksourceview5
  hicolor-icon-theme
  json-glib
  libadwaita
  libgee
  libsecret
  libsoup3
  libxml2
  pango
)
makedepends=(
  git
  meson
  vala
)

optdepends=(
  ## https://github.com/GeopJr/Tuba/issues/32#issuecomment-1483280966
  'webp-pixbuf-loader: calckey support (webp)'
)

source=("git+https://github.com/GeopJr/Tuba.git")
sha256sums=('SKIP')

pkgver() {
  cd Tuba
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^v//'
}

build() {
  arch-meson Tuba build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s "/usr/bin/dev.geopjr.Tuba" "$pkgdir/usr/bin/tuba" 
}
