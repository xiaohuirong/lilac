# Maintainer: Hoream <hoream@qq.com>
# Contributor: Solomon Choina <shlomochoina@gmail.com0
pkgname=wayfire-hoream-git
pkgver=0.8.0.20250709.133225.ga8fba6dd
pkgrel=1
pkgdesc="3D wayland compositor (hoream's version)"
arch=('x86_64' 'aarch64')
url="https://github.com/xiaohuirong/wayfire"
license=('MIT')
depends=('wlroots-hoream-git' 'cairo' 'pango' 'glm' 'libjpeg' 'wf-config-hoream-git' 'seatd')
makedepends=('git' 'meson' 'ninja' 'wayland-protocols' 'doctest' 'cmake')
optdepends=('wf-shell-hoream-git: GTK3-based panel for the Wayfire compositor'
            'wf-sound-control-git: Small utility for the Wayfire compositor to control sound volume')
provides=(wayfire-hoream)
conflicts=(wayfire-hoream)
replaces=()
options=(debug)
source=(wayfire::git+$url.git#branch=hoream
        wf-touch::git+https://github.com/WayfireWM/wf-touch
        wf-utils::git+https://github.com/WayfireWM/wf-utils
        wlroots::git+https://github.com/xiaohuirong/wlroots)

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
_prefix="/opt/wayfire-hoream"

prepare() {
    cd "$srcdir/wayfire"
    git config submodule.subprojects/wf-touch.url "${srcdir}/wf-touch"
    git -c protocol.file.allow=always submodule update --init subprojects/wf-touch

    git config submodule.subprojects/wf-utils.url "${srcdir}/wf-utils"
    git -c protocol.file.allow=always submodule update --init subprojects/wf-utils

    git config submodule.subprojects/wlroots.url "${srcdir}/wlroots"
    git -c protocol.file.allow=always submodule update --init subprojects/wlroots
}

pkgver () {
    cd "$srcdir/wayfire"
    (
        set -o pipefail
        version=$(grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | grep -oP "^\sversion:\s'\K[^']*")
        printf "${version}.%s.g%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)" "$(git log -1 --format='%h')"
    )
}

build() {
    cd "$srcdir/wayfire"
    # debug_ipc doesn't compile with wlroots 0.16.0
    export PKG_CONFIG_PATH=/opt/wlroots-hoream/lib/pkgconfig:/opt/wayfire-hoream/lib/pkgconfig:$PKG_CONFIG_PATH
    arch-meson \
        --buildtype=release \
        -Duse_system_wlroots=enabled \
        -Duse_system_wfconfig=enabled \
        -Dtests=disabled \
        -Ddebug_ipc=false \
        --prefix=${_prefix} \
        . \
        build
    ninja -C build
}

package() {
    cd "$srcdir/wayfire"
    DESTDIR="${pkgdir}/" ninja -C build install
    install -m644 wayfire.ini "${pkgdir}${_prefix}/share"
    install -d -m755 "${pkgdir}/etc/ld.so.conf.d"
    echo "${_prefix}/lib" > "${pkgdir}/etc/ld.so.conf.d/wayfire-hoream.conf"

    sed -i 's/wayfire/\/opt\/wayfire-hoream\/bin\/wayfire/g; s/Wayfire/Wayfire075/g' ${pkgdir}${_prefix}/share/wayland-sessions/wayfire.desktop
    install -d -m755 "${pkgdir}/usr/share/wayland-sessions"
    ln -sf ${_prefix}/share/wayland-sessions/wayfire.desktop "${pkgdir}/usr/share/wayland-sessions/wayfire075.desktop"
}

