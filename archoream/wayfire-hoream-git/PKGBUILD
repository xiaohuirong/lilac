# Maintainer: Hoream <hoream@qq.com>
# Contributor: Solomon Choina <shlomochoina@gmail.com0
pkgname=wayfire-hoream-git
pkgver=0.8.0.20240509.023836.g83daa255
pkgrel=12
pkgdesc="3D wayland compositor (hoream's version)"
arch=('x86_64' 'aarch64')
url="https://github.com/xiaohuirong/wayfire"
license=('MIT')
depends=('wlroots-hoream-git' 'cairo' 'pango' 'glm' 'libjpeg' 'wf-config-hoream-git' 'seatd')
makedepends=('git' 'meson' 'ninja' 'wayland-protocols' 'doctest' 'cmake')
optdepends=('wf-shell-hoream-git: GTK3-based panel for the Wayfire compositor'
            'wf-sound-control-git: Small utility for the Wayfire compositor to control sound volume')
provides=(wayfire wayfire-git)
conflicts=(wayfire wayfire-git)
replaces=()
options=(debug)
source=(wayfire::git+$url.git#branch=hoream
        wf-touch::git+https://github.com/WayfireWM/wf-touch
        wf-utils::git+https://github.com/WayfireWM/wf-utils
        wlroots::git+https://github.com/xiaohuirong/wlroots
        sdl_decor.patch
        decoration.patch
        fix_wofi_crash.patch
        xwayland_no_mapped.patch
        fix_mirror_crash.patch
        select_closest_refresh_rate.patch
        068e59749f94d9d303117a5e955c233c31fcca07.patch
        599ace91aca3f3c7a3faa66ce467c074c562fa12.patch
        54cd3b9a1b48072f19e3b6d1707c41a73dc7ef6b.patch
        support_custom_modes.patch
        wlr_custom_output.patch
        subsurface.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'dc7d68cc861cc85111e734baadc8ada99cfd99715cdef0b8fb2dd20c79bf08a1'
            '2b964484cbe7153d9417b4f885cd4ee7892b686a781c94611dae2f315a286164'
            '6a87a424869d37a219ab3bd23077c5da6dd443b51e9da599c68c73de372beb5b'
            '88fb119c3db7e544ae89448d040e49e7abaa772bd226cd4e847049f64fa54aac'
            'b7d734e31326ee0fa2277224d9cbfefb973b3e22bc2d53fcd699fafc8cc13376'
            '6959d2cfff2259acb8b42d12622c580a813f1e44a4642b6b5e9a5e3df1def0a7'
            'd350cc95d16c74311752b032fd615de8f11c5ca39781133dc23cc811978e0c3a'
            'b42554ce9ab61a2561e649bfff5f527baa94534cdda02ddbfbde14c523cd49ce'
            '599bbd5938a226acb09eb8f096d8c1f259ed4f1f6eef71e27b630823af9ad72f'
            'cab5edf604ba93c0c02706477bd80f063de04db1bef63e438b7462e052ee2ba7'
            '6c8d5d0281933b0467ecc355146109ee6090fd84d3c16793ea362b26cc1a0a02'
            '8ea0171d6d40f57d287a28b5dd09889ecdd5b79f225c3661407a804894f51e4b')

prepare() {
    cd "$srcdir/wayfire"
    git config submodule.subprojects/wf-touch.url "${srcdir}/wf-touch"
    git -c protocol.file.allow=always submodule update --init subprojects/wf-touch

    git config submodule.subprojects/wf-utils.url "${srcdir}/wf-utils"
    git -c protocol.file.allow=always submodule update --init subprojects/wf-utils

    git config submodule.subprojects/wlroots.url "${srcdir}/wlroots"
    git -c protocol.file.allow=always submodule update --init subprojects/wlroots
}

pkgver () {
    cd "$srcdir/wayfire"
    (
        set -o pipefail
        version=$(grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | grep -oP "^\sversion:\s'\K[^']*")
        printf "${version}.%s.g%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)" "$(git log -1 --format='%h')"
    )
}

build() {
    cd "$srcdir/wayfire"
    # debug_ipc doesn't compile with wlroots 0.16.0
    arch-meson \
        --buildtype=release \
        -Duse_system_wlroots=enabled \
        -Duse_system_wfconfig=enabled \
        -Dtests=disabled \
        -Ddebug_ipc=false \
        . \
        build
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/decoration.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/sdl_decor.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/fix_wofi_crash.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/xwayland_no_mapped.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/fix_mirror_crash.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/support_custom_modes.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/wlr_custom_output.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/select_closest_refresh_rate.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/068e59749f94d9d303117a5e955c233c31fcca07.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/599ace91aca3f3c7a3faa66ce467c074c562fa12.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/54cd3b9a1b48072f19e3b6d1707c41a73dc7ef6b.patch
    patch -b -d ${srcdir}/wayfire -Np1 -i ${srcdir}/subsurface.patch
    ninja -C build
}

package() {
    cd "$srcdir/wayfire"
    DESTDIR="${pkgdir}/" ninja -C build install
    install -m644 wayfire.ini "${pkgdir}/usr/share"
}

