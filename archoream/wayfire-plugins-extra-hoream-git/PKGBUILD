# Maintainer: Hoream <hoream@qq.com>

_pkgname=wayfire-plugins-extra
pkgname="${_pkgname}-hoream-git"
pkgver=r154.0cdbc73
pkgrel=8
pkgdesc="3D wayland compositor extra plugins"
arch=('any')
url="https://github.com/xiaohuirong/wayfire-plugins-extra"
license=('MIT')
depends=('wayfire-hoream-git' 'glibmm' 'iio-sensor-proxy')
makedepends=('git' 'meson' 'ninja' 'wayland-protocols')
optdepends=('wcm-hoream-git: GTK3-based configuration tool for the Wayfire compositor')
provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-git")
replaces=()
options=()
source=("${_pkgname}::git+https://github.com/xiaohuirong/wayfire-plugins-extra#commit=0cdbc738f4756156c102a1d86c72743d7d241ce7"
        "dbus::git+https://github.com/damianatorrpm/wayfire-plugin_dbus_interface"
        "wayfire-shadows::git+https://github.com/timgott/wayfire-shadows"
        "windecor::git+https://gitlab.com/wayfireplugins/windecor"
        "rename.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
install=wayfire-plugins-extra.install
prepare() {
  cd "$srcdir/$_pkgname"
  git config submodule.subprojects/dbus.url "${srcdir}/dbus"
  git -c protocol.file.allow=always submodule update --init subprojects/dbus
  
  git config submodule.subprojects/wayfire-shadows.url "${srcdir}/wayfire-shadows"
  git -c protocol.file.allow=always submodule update --init subprojects/wayfire-shadows

  git config submodule.subprojects/windecor.url "${srcdir}/windecor"
  git -c protocol.file.allow=always submodule update --init subprojects/windecor
}
pkgver() {
        cd "$srcdir/$_pkgname"

# Git, no tags available
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"

}

build() {
  patch -b -d ${srcdir}/${_pkgname} -Np1 -i ${srcdir}/rename.patch
  cd "$srcdir/$_pkgname"
  rm -rf build
  arch-meson build
  ninja -C build
}


package() {
        cd "$srcdir/${_pkgname}"
        DESTDIR="$pkgdir/" ninja -C build install
}
