# Maintainer: Hoream <hoream@qq.com>
pkgname=wcm-hoream-git
pkgver=0.8.0.r7.g9f871a8
pkgrel=4
pkgdesc='Wayfire Config Manager'
url=https://wayfire.org
arch=(x86_64)
license=(custom:MIT)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
depends=(gtkmm3 wayfire-hoream-git wf-shell-hoream-git)
makedepends=(wayland-protocols meson ninja git extra-cmake-modules)
optdepends=("wayfire-plugins-extra: configuration for extra Wayfire plugins")
source=("${pkgname}::git+https://github.com/WayfireWM/wcm#commit=9f871a8e2b189cabb50da2130386937097e713a3")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  ( set -o pipefail
    git describe --long --tag 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
  )
}

build () {
    export PKG_CONFIG_PATH=/opt/wlroots-hoream/lib/pkgconfig:/opt/wayfire-hoream/lib/pkgconfig:$PKG_CONFIG_PATH
	rm -rf build
	arch-meson \
    --prefix=/opt/wayfire-hoream \
    "${pkgname}" build
	ninja -C build
}

package () {
	DESTDIR="${pkgdir}" ninja -C build install
    sed -i 's|^Exec=.*|Exec=/opt/wayfire-hoream/bin/wdisplays|' ${pkgdir}/opt/wayfire-hoream/share/applications/network.cycles.wdisplays.desktop
    sed -i 's|^Icon=.*|Icon=/opt/wayfire-hoream/share/icons/hicolor/scalable/apps/network.cycles.wdisplays.svg|' ${pkgdir}/opt/wayfire-hoream/share/applications/network.cycles.wdisplays.desktop
    sed -i 's|^Exec=.*|Exec=/opt/wayfire-hoream/bin/wcm|' ${pkgdir}/opt/wayfire-hoream/share/applications/wayfire-config-manager.desktop
    sed -i 's|^Icon=.*|Icon=/opt/wayfire-hoream/share/icons/hicolor/scalable/apps/wayfire.svg|' ${pkgdir}/opt/wayfire-hoream/share/applications/wayfire-config-manager.desktop

    install -d ${pkgdir}/usr/share/applications
    ln -sf /opt/wayfire-hoream/share/applications/network.cycles.wdisplays.desktop ${pkgdir}/usr/share/applications/network.cycles.wdisplays.hoream.desktop
    ln -sf /opt/wayfire-hoream/share/applications/wayfire-config-manager.desktop ${pkgdir}/usr/share/applications/wayfire-config-manager-hoream.desktop
}

