# Maintainer: Hoream <hoream@qq.com>
_pkgname="firework-rs"
pkgname="${_pkgname}-git"
pkgver=r24.d3201fd
pkgrel=1
pkgdesc="a cross-platform ascii-art firework simulator in terminal."
url="https://github.com/Wayoung7/firework-rs"
license=("MIT")
arch=('x86_64')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=()
makedepends=("cargo" "git")
source=(${_pkgname}::git+${url})
sha256sums=('SKIP')

prepare() {
    cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

pkgver() {
  cd "${srcdir}/${_pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build(){
 	cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
    cargo build --example fountain --release
    cargo build --example vortex --release
    cargo build --example heart --release
}

check() {
    cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
	install -d "${pkgdir}/usr/bin"
	install -Dm755 "${srcdir}/${_pkgname}/target/release/firework" "${pkgdir}/usr/bin/firework"
	install -Dm755 "${srcdir}/${_pkgname}/target/release/examples/fountain" "${pkgdir}/usr/bin/firework-fountain"
	install -Dm755 "${srcdir}/${_pkgname}/target/release/examples/vortex" "${pkgdir}/usr/bin/firework-vortex"
	install -Dm755 "${srcdir}/${_pkgname}/target/release/examples/heart" "${pkgdir}/usr/bin/firework-heart"
    install -Dm644 "${srcdir}/${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
