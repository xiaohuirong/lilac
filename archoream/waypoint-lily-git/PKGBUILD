# Maintainer: Hoream <hoream@qq.com>
_pkgname="waypoint-lily"
pkgname="${_pkgname}-git"
pkgver=r55.a7e4b39
pkgrel=1
pkgdesc="Wayland clone of keynav "
url="https://github.com/lilydjwg/waypoint"
license=("MPL-2.0")
arch=('x86_64')
provides=("${_pkgname}" "waypoint")
conflicts=("${_pkgname}" "waypoint")
depends=("libxkbcommon" "gcc-libs" "glibc")
makedepends=("cargo" "git")
source=(${_pkgname}::git+${url})
sha256sums=('SKIP')

prepare() {
    cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
    sed -i 's/let mut color = Color::WHITE;/let mut color = Color::from_rgba8(135, 83, 238, 255);/g' "${srcdir}/${_pkgname}/src/main.rs"
    sed -i 's/color.apply_opacity(0.25);/color.apply_opacity(0.8);/g' "${srcdir}/${_pkgname}/src/main.rs"
    sed -i 's/let border_color = Color::WHITE;/let border_color = Color::from_rgba8(238, 83, 135, 255);/g' "${srcdir}/${_pkgname}/src/main.rs"
}

pkgver() {
  cd "${srcdir}/${_pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build(){
 	cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
	install -d "${pkgdir}/usr/bin"
	install -Dm755 "${srcdir}/${_pkgname}/target/release/waypoint" "${pkgdir}/usr/bin/waypoint"
    install -Dm644 "${srcdir}/${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
