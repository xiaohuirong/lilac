# Maintainer: Hoream <hoream@qq.com>
_pkgname="totalmapper"
pkgname="${_pkgname}-hoream-git"
pkgver=r194.25b5430
pkgrel=4
pkgdesc="A simple utility for remapping keys using the Linux event handling system."
arch=(x86_64)
url="https://github.com/ellbur/totalmapper"
license=('GPL3')
depends=()
provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-bin")
makedepends=('cargo' 'git' 'glibc' 'gcc-libs')
source=("${_pkgname}::git+https://github.com/xiaohuirong/${_pkgname}")
sha512sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
    ( set -o pipefail
      git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
	cd "${srcdir}/${_pkgname}"
	cargo build --release
}

package() {
	install -Dm755 "${srcdir}/${_pkgname}/target/release/totalmapper" "${pkgdir}/usr/bin/totalmapper"
	install -Dm644 -t "${pkgdir}/usr/share/doc/${_pkgname}/" "${srcdir}/${_pkgname}/README.md"
}
