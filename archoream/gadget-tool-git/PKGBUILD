# Maintainer: Hoream <hoream@qq.com>
_pkgname="gadget-tool"
pkgname="${_pkgname}-git"
pkgver=r148.8ebbf3e
pkgrel=1
pkgdesc="Gadget-tool - Linux command line tool for setting USB gadget using configFS"
url="https://github.com/linux-usb-gadgets/gt"
license=("Apache-2.0")
arch=('x86_64' 'aarch64')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=("libusbgx" "libconfig" "gcc-libs" "glibc")
makedepends=("git" "cmake" "pkgconf" "asciidoc")
optdepends=("adbd-linux: for adb gadget support"
"umtp-responder: for mtp gadget support")
source=(${_pkgname}::git+${url}
        "templates::git+https://github.com/kinsamanka/OpenStick-Builder.git"
        "linux_image_ADI-scripts::git+https://github.com/analogdevicesinc/linux_image_ADI-scripts.git"
        "acm-ecm-adb.scheme"
        "acm-ecm-mtp.scheme"
        "acm-ecm-mtp-adb.scheme"
        "acm-rndis-adb.scheme"
        "acm-rndis-mtp.scheme"
        "acm-rndis-mtp-adb.scheme"
        "acm-ncm-mtp.scheme"
        "gt@.service"
        "gt-load.sh"
        "gt-rm.sh")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'e846ff583db998348f61b9a7783ff17a44370244189403b91d7acb29ad64a873'
            'fb3c2db4e6a99f3bd84dc8d6bec05ba9b6f6b3c6352bf8e94d507e654472478d'
            '2ee08647f3fd5d0125dbdb0a8a6cf01e5780764199f551e0fece8696ef26933b'
            'edabc3c8b17437926b6fd2cda5e0a49d8475ef98298be7930d5fc629b6a45d38'
            '8819e50102d1ca76faa8a6c29fbf8ba83cb158c4c913adb046e30c310f4294fc'
            '26094278bc64e49c8db57b8c82b1290cb3b1c9a5246919944983c93f28062372'
            '10c8ad3d3bf4053842f2833001a824fa4dfd0eae0a8fda98471439f2bcb4124d'
            '2aaf54eba9bfe3a4a252a70c3e53322259dff07301ad5bf11c62d1f19e83efed'
            'c51c9397186cee90c603f071c021dd75cb5a8fa94aa27ad7965cdc1ea04eb9e0'
            '25cce5ac5b0bde13974a8089c022da365173279b375996bacd585eef8b546569')
backup=(
        "etc/gt/templates/acm-ecm-adb.scheme"
        "etc/gt/templates/acm-ecm-mtp.scheme"
        "etc/gt/templates/acm-ecm-mtp-adb.scheme"
        "etc/gt/templates/acm-ncm-mtp.scheme"
        "etc/gt/templates/acm-rndis-adb.scheme"
        "etc/gt/templates/acm-rndis-mtp.scheme"
        "etc/gt/templates/acm-rndis-mtp-adb.scheme"
        "etc/gt/templates/acm-ecm.scheme"
        "etc/gt/templates/acm.scheme"
        "etc/gt/templates/bluetooth.scheme"
        "etc/gt/templates/bmc.scheme"
        "etc/gt/templates/cdc-ecm.scheme"
        "etc/gt/templates/ecm.scheme"
        "etc/gt/templates/ffs.scheme"
        "etc/gt/templates/hid.scheme"
        "etc/gt/templates/mass.scheme"
        "etc/gt/templates/midi.scheme"
        "etc/gt/templates/midi2.scheme"
        "etc/gt/templates/rndis-os-desc.scheme"
        "etc/gt/templates/uac1.scheme"
        "etc/gt/templates/uac2.scheme"
        "etc/gt/templates/uvc.scheme"
        )

pkgver() {
  cd "${srcdir}/${_pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build(){
 	cd "${srcdir}/${_pkgname}"
    cd source
    sed -i 's/cmake_minimum_required(VERSION [0-9]\+\.[0-9]\+)/cmake_minimum_required(VERSION 3.5)/' CMakeLists.txt
    mkdir -p build
    cd build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
 	cd "${srcdir}/${_pkgname}"
    install -Dm644 examples/systemd/gt.target ${pkgdir}/usr/lib/systemd/system/gt.target
    install -Dm644 ${srcdir}/gt@.service ${pkgdir}/usr/lib/systemd/system/gt@.service
    install -D examples/systemd/99-udc.rules ${pkgdir}/usr/lib/udev/rules.d/99-udc.rules
    install -Dm755 ${srcdir}/gt-load.sh ${pkgdir}/usr/bin/gt-load.sh
    install -Dm755 ${srcdir}/gt-rm.sh ${pkgdir}/usr/bin/gt-rm.sh

    install -D LICENSE ${pkgdir}/usr/share/${pkgname}/LICENSE

    cd source/build
    make DESTDIR="$pkgdir/" PREFIX="/usr" MANDIR="/usr/share/man" install

    cd "${srcdir}/templates"
    cp configs/templates ${pkgdir}/etc/gt -r

    cd ${srcdir}/linux_image_ADI-scripts/usb-gadget-service/schemes
    cp *.scheme ${pkgdir}/etc/gt/templates

    install -Dm644 ${srcdir}/acm-ecm-mtp.scheme ${pkgdir}/etc/gt/templates/acm-ecm-mtp.scheme
    install -Dm644 ${srcdir}/acm-ecm-adb.scheme ${pkgdir}/etc/gt/templates/acm-ecm-adb.scheme
    install -Dm644 ${srcdir}/acm-ecm-mtp-adb.scheme ${pkgdir}/etc/gt/templates/acm-ecm-mtp-adb.scheme

    install -Dm644 ${srcdir}/acm-rndis-mtp.scheme ${pkgdir}/etc/gt/templates/acm-rndis-mtp.scheme
    install -Dm644 ${srcdir}/acm-rndis-adb.scheme ${pkgdir}/etc/gt/templates/acm-rndis-adb.scheme
    install -Dm644 ${srcdir}/acm-rndis-mtp-adb.scheme ${pkgdir}/etc/gt/templates/acm-rndis-mtp-adb.scheme

    install -Dm644 ${srcdir}/acm-ncm-mtp.scheme ${pkgdir}/etc/gt/templates/acm-ncm-mtp.scheme
}
