# Maintainer: Hoream <hoream@qq.com>

_pkgname=pcmanfm-qt
pkgname="${_pkgname}-patched-git"
pkgver=2.0.0.2.g749240b2
pkgrel=1
pkgdesc="The LXQt file manager, Qt port of PCManFM"
arch=("i686" "x86_64")
url="https://lxqt.org"
license=("GPL2")
depends=("desktop-file-utils" "libfm-qt-patched-git" "qt5-tools")
makedepends=("git" "lxqt-build-tools-git" "cmake")
provides=("$_pkgname=$pkgver" "$_pkgname-git")
conflicts=("$_pkgname" "$_pkgname-git")
optdepends=(
	"gvfs: for various features like mounting without root, remote filesystems, encrypted filesystems and many others mentioned on https://github.com/lxqt/pcmanfm-qt/wiki"
	"gvfs-mtp: MTP support (Media Transfer Protocol)"
	"lxqt-archiver: archive management"
	"qt5-translations: allow keyboard shortcuts to function in some languages (e.g. German)"
)

source=("git+https://github.com/lxqt/$_pkgname.git"
        "dialog.patch")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed "s/-/./g"
}

build() {
  patch -Np1 -i ${srcdir}/dialog.patch -d ${srcdir}/${_pkgname}

  mkdir -p build
  cd build

  cmake "$srcdir/$_pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
