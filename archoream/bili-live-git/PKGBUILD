# Maintainer: Hoream <hoream@qq.com>
_pkgname="bili-live"
pkgname="${_pkgname}-git"
pkgver=v0.1.1.r0.g4f1d3df
pkgrel=2
pkgdesc="A command line tool for starting and stopping live streams on bilibili.com, capable of providing the RTMP address and stream key for streaming software such as OBS."
url="https://github.com/HellOwhatAs/bili-live"
license=("custom")
arch=('x86_64' 'aarch64')
provides=("$_pkgname")
conflicts=("$_pkgname")
depends=()
makedepends=("cargo" "git")
source=("$_pkgname::git+$url")
sha256sums=('SKIP')

prepare() {
	cd "${srcdir}/${_pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

pkgver() {
	cd "${srcdir}/${_pkgname}"
	(
		set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
			printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cd "${srcdir}/${_pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features
}

check() {
	cd "${srcdir}/${_pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	cargo test --frozen --all-features
}

package() {
	install -d "${pkgdir}/usr/bin"
	install -Dm755 "${srcdir}/${_pkgname}/target/release/${_pkgname}" "${pkgdir}/usr/bin/${pkg_name}"
}
