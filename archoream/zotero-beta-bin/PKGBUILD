# Maintainer: Hoream <horeawm@qq.com>

pkgname=zotero-beta-bin
pkgver=7.0.0.r29.f0e1da23b
pkgrel=1
pkgdesc="Zotero is a free, easy-to-use tool to help you collect, organize, annotate, cite, and share research."
arch=('x86_64')
url="https://www.zotero.org/download"
license=('AGPL3')
depends=('dbus-glib' 'gtk3' 'nss' 'libxt')
provides=('zotero')
conflicts=('zotero' 'zotero-bin')
replaces=('zotero')


install='zotero.install'

source=("zotero.desktop"
        "zotero-beta03.tar.bz2::https://www.zotero.org/download/client/dl?platform=linux-x86_64&channel=beta")
sha256sums=('eab76db7a56a4d9aaa17baaf240b82fcf57944a4ddf8ef1b58cc64182426cedc'
            'SKIP')

pkgver(){
    cd "$srcdir"/Zotero_linux-$CARCH/app
    full_ver=$(grep -oP '^Version=\K[^ ]+' application.ini)
    version=$(echo "$full_ver" | grep -oP '^\d+\.\d+\.\d+')
    build=$(echo "$full_ver" | grep -oP '(?<=beta\.)\d+')
    hash=$(echo "$full_ver" | grep -oP '(?<=\+)\w+$')
    printf "$version.r$build.$hash"
}

package() {
  install -dDm755 "$pkgdir"/usr/{bin,lib/zotero}
  mv "$srcdir"/Zotero_linux-$CARCH/* "$pkgdir"/usr/lib/zotero
  ln -s /usr/lib/zotero/zotero "$pkgdir"/usr/bin/zotero
  install -Dm644 "$srcdir"/zotero.desktop "$pkgdir"/usr/share/applications/zotero.desktop
  # Copy zotero icons to a standard location
  install -Dm644 "$pkgdir"/usr/lib/zotero/chrome/icons/default/default16.png "$pkgdir"/usr/share/icons/hicolor/16x16/apps/zotero.png
  install -Dm644 "$pkgdir"/usr/lib/zotero/chrome/icons/default/default32.png "$pkgdir"/usr/share/icons/hicolor/32x32/apps/zotero.png
  install -Dm644 "$pkgdir"/usr/lib/zotero/chrome/icons/default/default48.png "$pkgdir"/usr/share/icons/hicolor/48x48/apps/zotero.png
  install -Dm644 "$pkgdir"/usr/lib/zotero/chrome/icons/default/default256.png "$pkgdir"/usr/share/icons/hicolor/256x256/apps/zotero.png
  # Disable APP update
  # sed -i '/pref("app.update.enabled", true);/c\pref("app.update.enabled", false);' "$pkgdir"/usr/lib/zotero/defaults/preferences/prefs.js
  # No need to keep a shell around when launching Zotero
  sed -i -r 's/^("\$CALLDIR\/zotero-bin" -app "\$CALLDIR\/app\/application.ini" "\$@")/exec \1/' "$pkgdir"/usr/lib/zotero/zotero
}
