# Maintainer: Hoream <hoream@qq.com>
pkgname=wf-config-hoream-git
pkgver=r174.e42a387
pkgrel=11
pkgdesc="A library for managing configuration files, written for wayfire"
arch=('x86_64' 'aarch64')
url="https://github.com/WayfireWM/wf-config"
license=('MIT')
depends=('libevdev' 'libxml2')
makedepends=('git' 'wlroots-hoream-git' 'meson' 'ninja' 'wayland-protocols' 
              'glm' 'doctest' 'cmake')
provides=(wf-config)
#conflicts=("wf-config" "wf-config-git")
replaces=()
source=("${pkgname}::git+https://github.com/WayfireWM/wf-config#commit=e42a3870fb194842a505ad5a9671be1aebda0b0b")
sha256sums=('SKIP')

pkgver () {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${srcdir}/${pkgname}"
  sed -i '/#include <set>/a #include <algorithm>' src/file.cpp
}

build() {
    arch-meson \
    --prefix=/opt/wayfire-hoream \
    "$srcdir/${pkgname}/" \
    build
    ninja -C build
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
}
