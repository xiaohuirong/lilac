# Maintainer: Hoream <hoream@qq.com>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>
# Contributor: Antonin DÃ©cimo <antonin dot decimo at gmail dot com>
pkgname=wlroots-hoream-git
pkgver=0.17.0.20241009.152601
pkgrel=5
license=(MIT)
pkgdesc='Modular Wayland compositor library (git version, with patches)'
url=https://gitlab.freedesktop.org/wlroots/wlroots
arch=(x86_64 aarch64)
#provides=("libwlroots.so" "wlroots=${pkgver%%.r*}")
conflicts=(wlroots-hoream)
options=(debug)
depends=(
	glslang
	libinput
	libxcb
    mesa
	xcb-util-errors
	xcb-util-renderutil
	xcb-util-wm
        libdisplay-info
        libliftoff

        'libpixman-1.so'
        'libseat.so'
        'libudev.so'
        'libvulkan.so'
        'libwayland-client.so'
        'libwayland-server.so'
        'libxkbcommon.so'
        'libdisplay-info.so'
        'libliftoff.so'

	xorg-xwayland)
optdepends=('xorg-xwayland-lily: HiDPI support, see https://gitlab.freedesktop.org/xorg/xserver/-/merge_requests/733')
makedepends=(
	git
	meson
	vulkan-headers
	wayland-protocols
	xorgproto)
source=("${pkgname}::git+https://github.com/xiaohuirong/wlroots#branch=hoream")
sha512sums=('SKIP')
_prefix="/opt/wlroots-hoream"

pkgver () {
  cd "${pkgname}"
  (
     set -o pipefail
     version=$(grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | grep -oP "^\sversion:\s'\K[^-']*")
     printf "${version}.%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
  )
}

build () {
  arch-meson \
    --buildtype=release \
    -Dwerror=false \
    -Dexamples=false \
    --prefix=${_prefix} \
    "${pkgname}" build
  meson compile -C build
}

package () {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm644 "${pkgname}/"LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d -m755 "${pkgdir}/etc/ld.so.conf.d"
  echo "${_prefix}/lib" > "${pkgdir}/etc/ld.so.conf.d/wlroots-hoream.conf"
}
