# Maintainer: Dan Johansen <strit@strits.dk>

_pkgname=status-notifier
pkgbase=dfl-status-notifier
pkgname=(
        'dfl-status-notifier'
        'dfl-status-notifier-qt6'
)
pkgver=0.2.0
pkgrel=2
pkgdesc="Implementation of the XDG Status Notification Specifications"
arch=('x86_64' 'aarch64')
url="https://gitlab.com/desktop-frameworks/$_pkgname"
license=('GPL-3.0-only')
makedepends=(
            'meson'
            'ninja'
            'qt5-base'
            'qt6-base'
)
source=("$url/-/archive/v${pkgver}/${_pkgname}-v${pkgver}.tar.gz")
sha256sums=('935cb2475d27374cc8a76bf8a8dbdf960c59843c3809451a872e9be81f2cfbc5')

build() {
  cd "${_pkgname}-v${pkgver}"
  echo "Building QT5 version..."
  meson .build --prefix=/usr --buildtype=release
  ninja -C .build
  
  echo "Building QT6 version..."
  meson .build-qt6 --prefix=/usr -Duse_qt_version=qt6 --buildtype=release
  ninja -C .build-qt6
}

package_dfl-status-notifier() {
  depends=('qt5-base')
  cd "${_pkgname}-v${pkgver}"
  DESTDIR="${pkgdir}" ninja -C .build install
}

package_dfl-status-notifier-qt6() {
  depends=('qt6-base')
  cd "${_pkgname}-v${pkgver}"
  DESTDIR="${pkgdir}" ninja -C .build-qt6 install
}
