# Maintainer: Ecmel Berk CanlÄ±er <me@ecmelberk.com>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: philefou <tuxication AT gmail DOT com>
# Contributor: lindquist <tomas@famolsen.dk>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>
# Contributor: Mihai Militaru <mihai.militaru@ephemeros.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_pkgname=glfw
pkgname="${_pkgname}-wayland-minecraft-git"
pkgdesc="A free, open source, portable framework for graphical application development (wayland, patched for Minecraft)"
pkgver=3.3.r781.g3eaf1255
pkgrel=2
arch=('x86_64')
url="https://github.com/Admicos/minecraft-wayland"
license=('custom:ZLIB')
depends=('wayland' 'libxkbcommon' 'libgl')
conflicts=('glfw' 'glfw-wayland' 'glfw-wayland-minecraft')
provides=("glfw=$pkgver")
makedepends=('git' 'mesa' 'cmake' 'doxygen' 'vulkan-headers' 'vulkan-icd-loader'
             'extra-cmake-modules' 'wayland-protocols' 'libxi' 'libxrandr'
             'libxcursor' 'libxkbcommon' 'libxinerama')
source=("${_pkgname}::git+https://github.com/glfw/glfw"
        "glfw.patch")
sha512sums=('SKIP'
        'SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^v//'
}


prepare() {
  cd "$srcdir/$_pkgname"
  rm -rf build-wayland || true
  mkdir build-wayland

  patch -p1 < "$srcdir/glfw.patch"
}

build() {
  cd "$srcdir/${_pkgname}/build-wayland"

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON \
      -DGLFW_USE_WAYLAND=ON \
      -DGLFW_BUILD_EXAMPLES=no \
      -DGLFW_BUILD_TESTS=no \
      -DGLFW_BUILD_DOCS=no
}

package() {
  cd "$srcdir/$_pkgname"/build-wayland

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
