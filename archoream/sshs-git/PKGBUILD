# Maintainer: Hoream <hoream@qq.com>
_pkgname="sshs"
pkgname="${_pkgname}-git"
pkgver=4.3.0.r0.g116cb8d
pkgrel=1
pkgdesc="Terminal user interface for SSH"
url="https://github.com/quantumsheep/sshs"
license=("MIT")
arch=('x86_64')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=()
makedepends=("cargo" "git")
source=(${_pkgname}::git+${url})
sha256sums=('SKIP')

prepare() {
    cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
 	cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd "${srcdir}/${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
	install -d "${pkgdir}/usr/bin"
	install -Dm755 "${srcdir}/${_pkgname}/target/release/${_pkgname}" "${pkgdir}/usr/bin/${pkg_name}"
    install -Dm644 "${srcdir}/${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
