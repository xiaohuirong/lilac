# Maintainer: Lex Black <autumn-wind at web dot de>
# Contributor: Daniel Larsson <znixen@live.se>

pkgbase=patool
pkgname='patool'
pkgver=1.15.0
pkgrel=1
pkgdesc="portable command line archive file manager"
arch=('any')
url="https://wummel.github.io/patool/"
license=('GPL')
depends=('python')
makedepends=(python-build python-installer python-wheel python-setuptools)
optdepends=("lz4: extracting LZ4 archives"
    "p7zip: extracting ZIP and 7z files"
    "unarchiver: extracting various formats"
    "unrar: extracting RAR files"
    "zstd: extracting ZSTANDARD files")
#source=("https://pypi.python.org/packages/source/p/$pkgbase/$pkgbase-$pkgver.tar.gz")
source=("$pkgbase-$pkgver.tar.gz::https://github.com/wummel/patool/archive/upstream/${pkgver}.tar.gz")
sha256sums=('4b4e51ae6422d65f161cba10d155fa877c4d5328eec595d86f884983ecefec48')


build() {
  cd "${pkgbase}-upstream-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${pkgbase}-upstream-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
