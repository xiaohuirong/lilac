# Maintainer: Hoream <hoream@qq.com>

pkgname=umtp-responder-git
pkgver=r377.00fc22259f
pkgrel=1
pkgdesc="Lightweight USB Media Transfer Protocol (MTP) responder daemon for GNU/Linux "
arch=('x86_64' 'aarch64')
url="https://github.com/viveris/uMTP-Responder"
license=('GPL-3.0')
source=("git+$url")
sha256sums=('SKIP')
depends=('glib2' 'systemd-libs')
makedepends=('make' 'git')
conflicts=('umtp-responder')
provides=("umtp-responder")
backup=("etc/umtprd/umtprd.conf")

pkgver() {
  cd uMTP-Responder
  printf 'r%d.%s' \
    $(git rev-list --count HEAD) \
    $(git rev-parse --short=10 HEAD)
}

build() {
  cd uMTP-Responder
  make
}

package() {
  cd uMTP-Responder
  install -Dm755 umtprd ${pkgdir}/usr/bin/umtprd
  install -Dm755 conf/umtprd-ffs.sh ${pkgdir}/usr/bin/umtprd-ffs.sh
  install -D conf/umtprd.conf ${pkgdir}/etc/umtprd/umtprd.conf
}
